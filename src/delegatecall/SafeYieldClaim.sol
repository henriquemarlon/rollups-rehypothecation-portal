// SPDX-License-Identifier: MIT

pragma solidity ^0.8.27;

import {IERC4626} from "@openzeppelin-contracts-5.2.0/interfaces/IERC4626.sol";

/// @title SafeYieldClaim
/// @notice A delegatecall implementation for claiming yield from ERC-4626 vaults.
/// @dev This contract is designed to be called via delegatecall from the Cartesi application. It calculates the yield generated by deposited assets and withdraws only the yield portion, leaving user deposits intact.
contract SafeYieldClaim {
    /// @notice Claims accumulated yield from an ERC-4626 vault
    /// @dev This function should be called via delegatecall from the application contract.
    /// It calculates yield as: current vault value - total supply managed off-chain.
    /// In delegatecall context, address(this) is the application holding the vault shares.
    /// @param vault The ERC-4626 vault holding the deposited assets
    /// @param receiver The address that will receive the claimed yield
    /// @param totalSupplyOffChain The total supply managed off-chain by the Cartesi machine (sum of all user deposits)
    /// @return yieldClaimed The amount of yield tokens withdrawn
    function safeClaim(IERC4626 vault, address receiver, uint256 totalSupplyOffChain)
        public
        returns (uint256 yieldClaimed)
    {
        uint256 rehypothecatedShares = vault.balanceOf(address(this));
        uint256 rehypothecatedAssets = vault.convertToAssets(rehypothecatedShares);
        yieldClaimed = rehypothecatedAssets > totalSupplyOffChain ? rehypothecatedAssets - totalSupplyOffChain : 0;

        if (yieldClaimed > 0) {
            vault.withdraw(yieldClaimed, receiver, address(this));
        }

        return yieldClaimed;
    }
}
